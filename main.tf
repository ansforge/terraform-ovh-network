terraform {
  required_providers {
    ovh       = { source = "ovh/ovh", version = ">= 2.11.0" }
    openstack = { source = "terraform-provider-openstack/openstack", version = ">= 1.53.0" }
    vault     = { source = "hashicorp/vault", version = ">= 3.25.0" }
  }
}

provider "vault" {
  skip_child_token = true
}

ephemeral "vault_kv_secret_v2" "ovh" {
  mount = "iacrunner-amont"
  name  = "ovh_key"
}

ephemeral "vault_kv_secret_v2" "os" {
  mount = "iacrunner-amont"
  name  = "openstack_key"
}

locals {
  ovh_creds = ephemeral.vault_kv_secret_v2.ovh.data
  os_creds  = ephemeral.vault_kv_secret_v2.os.data
}

provider "ovh" {
  endpoint           = "ovh-eu"
  application_key    = local.ovh_creds["OVH_APPLICATION_KEY"]
  application_secret = local.ovh_creds["OVH_APPLICATION_SECRET"]
  consumer_key       = local.ovh_creds["OVH_CONSUMER_KEY"]
}

provider "openstack" {
  auth_url                      = local.os_creds["OS_AUTH_URL"]
  application_credential_id      = local.os_creds["OS_APPLICATION_CREDENTIAL_ID"]
  application_credential_secret = local.os_creds["OS_APPLICATION_CREDENTIAL_SECRET"]
  region                        = var.region
}

module "network" {
  source       = "./modules/network"
  service_name = var.service_name
  region       = var.region
  vlans        = var.vlans
}
